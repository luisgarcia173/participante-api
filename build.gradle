plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'jacoco'
}

group = 'com.luisgarcia'
version = '1.0.0'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	// SpringBoot Starter
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// OpenAPI
	implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.3.0'

	// Lombok Processor
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// Guava Libs - Collections Handler
	implementation group: 'com.google.guava', name: 'guava', version: '33.0.0-jre'

	// H2 Database
	runtimeOnly 'com.h2database:h2'

	// Tests
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.testcontainers:junit-jupiter'
}

jacoco {
	toolVersion = "0.8.11"
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = 'CLASS'
			excludes = [
					'com.luisgarcia.controllers.handler.*',
					'com.luisgarcia.controllers.dtos.*',
					'com.luisgarcia.repositories.entities.*',
					'com.luisgarcia.ParticipanteApiApplication'
			]
			limit {
				counter = 'INSTRUCTION'
				value = 'COVEREDRATIO'
				minimum = 0.9
			}
		}
	}
}

jacocoTestReport {
	reports {
		html.destination file("${buildDir}/jacocoHtml")
	}
	dependsOn test
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					'com/luisgarcia/controllers/handler/**',
					'com/luisgarcia/controllers/dtos/**',
					'com/luisgarcia/repositories/entities/**',
					'com/luisgarcia/ParticipanteApiApplication'
			])
		}))
	}
	finalizedBy jacocoTestCoverageVerification
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}
